// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© aminmasood54

//@version=5
strategy("BodyWeightedRSI",
         default_qty_type = strategy.percent_of_equity,default_qty_value = 100,
         commission_type = strategy.commission.percent,commission_value = 0.02)

RSILen = input.int(title = 'RSILen',defval = 7)
N1 = input.int(title = 'Len1',defval = 87)
N2 = input.int(title = 'Len2',defval = 30)
N3 = input.int(title = 'Len3',defval = 90)
start_time = input.time(defval=timestamp("1 Apr 2023 00:00 +0300"),confirm = true)
end_time = input.time(defval=timestamp("1 May 2023 00:00 +0300"),confirm = true)
Percent = input.float(2, title="SL%")
// ===========================================================
Body = math.abs(close-open)
RSI = ta.rsi(close,RSILen)
GetLine(Length)=>
    SBody = math.sum(Body,Length)
    Line = 0.0
    for i=0 to Length-1
        Line := Line+RSI[i]*Body[i]/SBody
    Line

Line1 = GetLine(N1)
Line2 = GetLine(N1+N2)
Line3 = GetLine(N1+N2+N3)

plot(Line1,title = 'Line1',color = color.blue,linewidth = 2)
plot(Line2,title = 'Line2',color = color.red,linewidth = 2)
plot(Line3,title = 'Line2',color = color.black,linewidth = 2)
// =============================================
LCond = ta.crossover(Line1,Line2) and Line2>Line3
SCond = ta.crossunder(Line1,Line2) and Line2<Line3
CloseLong = ta.crossunder(Line1,Line2)
CloseShort = ta.crossover(Line1,Line2)
LEON = strategy.position_size>0
SEON = strategy.position_size<0
SLL = 0.0
SLS = 0.0
TimeCond= time>=start_time and time<=end_time
if LCond and TimeCond
    strategy.entry('Long',strategy.long)
    SLL := (1-Percent/100)*close
if LEON
    SLL := SLL[1]
if CloseLong or (TimeCond==false and LEON)
    strategy.close('Long')
if SCond and TimeCond
    strategy.entry('Short',strategy.short)
    SLS := (1+Percent/100)*close
if SEON
    SLS := SLS[1]
if CloseShort or (TimeCond==false and SEON)
    strategy.close('Short')

strategy.exit('ExL','Long',stop = SLL)
strategy.exit('ExS','Short',stop = SLS)
// ===========================================================================================================================================================
Lev = input.int(defval=10,title='Levrage')
commision = input.float(defval=0.02,title='Commision %')
avgGrossProfit(val) =>
    Profit = 0.0
    if strategy.closedtrades.size(val)>0
        Profit := 100*(strategy.closedtrades.exit_price(val)-strategy.closedtrades.entry_price(val))/strategy.closedtrades.entry_price(val)
    else
        Profit :=100*(-strategy.closedtrades.exit_price(val)+strategy.closedtrades.entry_price(val))/strategy.closedtrades.entry_price(val)
    Profit :=  Profit - 2*commision
PrfPT = avgGrossProfit(strategy.closedtrades-1)
EquityVal=1.0
if strategy.closedtrades==0
    EquityVal:=1
else
    if strategy.closedtrades>strategy.closedtrades[1]
        EquityVal:=(1+Lev*PrfPT/100)*EquityVal[1]
    else
        EquityVal:=EquityVal[1]
var testTable = table.new(position = position.top_right, columns = 2, rows = 1, bgcolor = color.yellow, border_width = 1)
if barstate.isconfirmed
    table.cell(table_id = testTable, column = 0, row = 0, text = "Profit is ")
    table.cell(table_id = testTable, column = 1, row = 0, text = str.tostring((EquityVal-1)*100)+'%', bgcolor=color.teal)
// ===========================================================================================================================================================

